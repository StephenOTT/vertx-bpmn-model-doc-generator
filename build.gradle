plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '2.0.2'
  id "com.moowork.node" version "1.2.0" //https://github.com/srs/gradle-node-plugin
}

repositories {
  jcenter()
}

version = '0.1.0'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'
mainClassName = 'io.vertx.core.Launcher'

def vertxVersion = '3.5.1'
def mainVerticleName = 'entryPointVerticle.js'
def watchForChange = 'app/**'
def doOnChange = './gradlew classes'

dependencies {
  compile "io.vertx:vertx-core:$vertxVersion"
  compile "io.vertx:vertx-lang-js:$vertxVersion" // JS Lang

  compile "org.apache.commons:commons-io:1.3.2"


  compile "org.freemarker:freemarker:2.3.27-incubating"
  compile "org.camunda.bpm.model:camunda-bpmn-model:7.8.0"

  testCompile "junit:junit:4.12"
  testCompile "io.vertx:vertx-unit:$vertxVersion"
}

processResources {
  from 'app'
  from ('node_modules') {into 'node_modules'}
}

shadowJar {
  classifier = 'fat'
  manifest {
      attributes "Main-Verticle": mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

run {
  args = ['run', mainVerticleName,
              "--redeploy=$watchForChange",
              "--launcher-class=$mainClassName",
              "--on-redeploy=$doOnChange",
          ]
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.6.0'
}